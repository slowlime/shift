const N = 2
const MAX = 511

enum State {
  Unsynchronized,
  Entering,
  Choosing,
  Acquiring,
  Acquired,
  AwaitingAcquisition,
  WaitingInLine,
  BreakingTies,
  Critical,
  Releasing,
}

var acquiring: [bool; N] = [false; N]
var ticket: [0..MAX; N] = [0; N]
var i: [0..N - 1; N]
var next_ticket: [0..MAX; N]
var state: [State; N] = [State::Unsynchronized; N]
var stall: [bool; N] = [false; N]

trans {
  const for self_idx in 0..N {
    alias stall = stall[self_idx]

    // No stalling:
    stall <- false

    // Alternating stalling:
    // stall <- !stall

    // Non-deterministic stalling:
    // either {
    //   stall <- true
    // } or {
    //   stall <- false
    // }

    defaulting {
      alias acquiring = acquiring[self_idx]
      alias ticket = ticket[self_idx]
      alias i = i[self_idx]
      alias next_ticket = next_ticket[self_idx]
      alias state = state[self_idx]
    } in {
      unless stall {
        match state {
          State::Unsynchronized => {
            state <- State::Entering
          }

          State::Entering => {
            acquiring <- true
            i <- 0
            next_ticket <- 0
            state <- State::Choosing
          }

          State::Choosing => {
            next_ticket <- max(next_ticket, ::ticket[i])

            if i == N {
              state <- State::Acquiring
            } else {
              i <- i + 1
            }
          }

          State::Acquiring => {
            ticket <- next_ticket + 1
            state <- State::Acquired
          }

          State::Acquired => {
            acquiring <- false
            i <- 0
            state <- State::AwaitingAcquisition
          }

          State::AwaitingAcquisition => {
            if i == self_idx {
              if i < N - 1 {
                i <- i + 1
              } else {
                state <- State::Critical
              }
            } else if !::acquiring[i] {
              state <- State::WaitingInLine
            }
          }

          State::WaitingInLine => {
            unless ::ticket[i] != 0 && ::ticket[i] < ticket {
              if ::ticket[i] == ticket && i < self_idx {
                state <- State::BreakingTies
              } else if i < N - 1 {
                i <- i + 1
                state <- State::AwaitingAcquisition
              } else {
                state <- State::Critical
              }
            }
          }

          State::BreakingTies => {
            unless ::ticket[i] != 0 {
              if i < N - 1 {
                i <- i + 1
                state <- State::AwaitingAcquisition
              } else {
                state <- State::Critical
              }
            }
          }

          State::Critical => {
            state <- State::Releasing
          }

          State::Releasing => {
            ticket <- 0
            state <- State::Unsynchronized
          }
        }
      }
    }
  }
}
